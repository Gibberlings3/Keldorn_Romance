
// b!keldorn_rom_d.baf - Keldorn's night talks 

IF 
    InParty(Myself) 
    RealGlobalTimerExpired("B!KelTime","GLOBAL") 
    Global("B!KeldornRA","GLOBAL",1) 
    !AreaType(DUNGEON) 
    !Global("Chapter","GLOBAL",%bg2_chapter_5%) 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    CombatCounter(0) 
    !See([ENEMY]) 
    Global("B!KelLT","GLOBAL",15) 
THEN 
    RESPONSE #100 
        IncrementGlobal("B!KelLT","GLOBAL",1) 
        PlaySong(0) 
        PlaySound("bkelsong") 
        StartDialogueNoSet(Player1) 
END 

IF 
    InParty(Myself) 
    Global("B!KelToBLT","GLOBAL",0) 
    Global("B!KR_ToBc1","GLOBAL",2)
    !Global("B!KeldornRA","GLOBAL",0) 
    !Global("B!KeldornRA","GLOBAL",3) 
//    AreaCheck("AR4500") 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    CombatCounter(0) 
    !See([ENEMY]) 
THEN 
    RESPONSE #100 
        SetGlobal("B!KelToBLT","GLOBAL",2) 
        PlaySong(0) 
        PlaySound("bkelsong") 
        StartDialogueNoSet(Player1) 
END 

IF 
    InParty(Myself) 
    RealGlobalTimerExpired("B!KelTime","GLOBAL") 
    TimeOfDay(NIGHT) 
    Global("B!KeldornRA","GLOBAL",1) 
    !AreaType(DUNGEON) 
    !Global("Chapter","GLOBAL",5) 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    CombatCounter(0) 
    !See([ENEMY]) 
    Global("B!KelLT","GLOBAL",67) 
	GlobalLT("Chapter","GLOBAL",8) //not in ToB
THEN 
    RESPONSE #100 
        IncrementGlobal("B!KelLT","GLOBAL",1) 
        PlaySong(0) 
        PlaySound("bkelsong") 
        StartDialogueNoSet(Player1) 
END 

IF 
    InParty(Myself) 
    Global("B!KR_MWLovedOne","GLOBAL",2) 
    See(Player1) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
THEN 
    RESPONSE #100 
        PlaySong(0) 
        PlaySound("bkelsong") 
        SetGlobal("B!KR_MWLovedOne","GLOBAL",3) 
        StartDialogueNoSet(Player1) 
END 

IF 
    InParty(Myself) 
    Global("BeginChallenge4","GLOBAL",6) 
    Global("B!KR_ToBc4","GLOBAL",0) 
    Global("B!KeldornRA","GLOBAL",2) 
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY]) 
THEN 
    RESPONSE #100 
        PlaySong(0) 
        PlaySound("bkelsong") 
        SetGlobal("B!KR_ToBc4","GLOBAL",1) 
        StartDialogueNoSet(Player1) 
END 

IF 
    InParty(Myself) 
    Global("SaradushSacked","GLOBAL",1)
    Global("B!KelSaradushFell","GLOBAL",0) 
    Global("B!KeldornRA","GLOBAL",2) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
    See(Player1) 
    CombatCounter(0) 
THEN 
    RESPONSE #100 
        PlaySong(0) 
        PlaySound("bkelsong") 
        SetGlobal("B!KelSaradushFell","GLOBAL",1) 
        StartDialogueNoSet(Player1) 
END 

IF 
    InParty(Myself) 
//    RealGlobalTimerExpired("B!KelTime","GLOBAL") 
    Global("B!KeldornRA","GLOBAL",1) 
    !AreaType(DUNGEON) 
    !Global("Chapter","GLOBAL",5) 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    CombatCounter(0) 
    !See([ENEMY]) 
    OR(2) 
        Global("B!KelLT","GLOBAL",11) 
        Global("B!KelLT","GLOBAL",53) 
    OR(11) 
        AreaCheck("AR0021")       // Crooked Crane, 1st floor
        AreaCheck("AR0313")       // Sea's Bounty, 1st floor 
        AreaCheck("AR0406")       // Copper Coronet 
        AreaCheck("AR0509")       // Five Flagons, 1st floor 
        AreaCheck("AR0513")       // Delosar's, 1st floor 
        AreaCheck("AR0522")       // Five Flagons, 1st floor (stronghold) 
        AreaCheck("AR0704")       // Mithrest Inn 
        AreaCheck("AR0709")       // Den of the Seven Vales 
        AreaCheck("AR1105")       // Imnesvale Inn 
        AreaCheck("AR1602")       // Vulgar Monkey 
        AreaCheck("AR2010")       // Trademeet Inn 
THEN 
    RESPONSE #100 
        IncrementGlobal("B!KelLT","GLOBAL",1) 
        PlaySong(0) 
        PlaySound("bkelsong") 
        StartDialogueNoSet(Player1) 
END 

/* Post-confession night talks */ 

IF 
    InParty(Myself) 
    GlobalTimerExpired("B!KelNightTime","GLOBAL") 
    Global("B!KeldornRA","GLOBAL",1) 
    !AreaType(DUNGEON) 
    !Global("Chapter","GLOBAL",5) 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    CombatCounter(0) 
    !See([ENEMY]) 
	GlobalLT("Chapter","GLOBAL",8) //not in ToB
    OR(4) 
        Global("B!KelNT","GLOBAL",1) 
        Global("B!KelNT","GLOBAL",3) 
        Global("B!KelNT","GLOBAL",5) 
        Global("B!KelNT","GLOBAL",7) 
THEN 
    RESPONSE #100 
        IncrementGlobal("B!KelNT","GLOBAL",1) 
        PlaySong(0) 
        PlaySound("bkelsong") 
        StartDialogueNoSet(Player1) 
END 

IF 
    InParty(Myself) 
    Global("B!KelBandage","LOCALS",1) 
    Global("B!KeldornRA","GLOBAL",1) 
    !StateCheck(Myself,CD_STATE_NOTVALID) 
    !StateCheck(Player1,CD_STATE_NOTVALID) 
    CombatCounter(0) 
    !See([ENEMY]) 
	GlobalLT("Chapter","GLOBAL",8) //not in ToB
THEN 
    RESPONSE #100 
        IncrementGlobal("B!KelBandage","LOCALS",1) 
        PlaySong(0) 
        PlaySound("bkelsong") 
        StartDialogueNoSet(Player1) 
END 
