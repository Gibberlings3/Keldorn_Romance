
// b!zashx.baf - Zasheida's combat script 

IF	
    AttackedBy([GOODCUTOFF],DEFAULT) 
	Allegiance(Myself,NEUTRAL) 
THEN 
	RESPONSE #100 
	    Enemy() 
END 

IF 
    Global("B!Hostile","LOCALS",0) 
    OR(2) 
          Global("B!KelQuestFight","GLOBAL",1) 
          Global("B!KelQuestBetray","GLOBAL",1) 
THEN 
    RESPONSE #100 
    	EquipMostDamagingMelee()
        Enemy() 
        SetGlobal("B!Hostile","LOCALS",1) 
END 

IF	
    AttackedBy([ANYONE],DEFAULT) 
THEN 
	RESPONSE #100 
    	DisplayStringHead(Myself,46150) 
    	ReallyForceSpell(Myself,WIZARD_INVISIBILITY) 
    	Continue() 
END 

IF 
    HPPercentLT(Myself,50) 
    !GlobalTimerNotExpired("UsePotion","LOCALS") 
    HasItem("potn52",Myself) 
THEN 
    RESPONSE #100 
        DisplayStringHead(Myself,46150) 
        UseItem("potn52",Myself) 
        SetGlobalTimer("UsePotion","LOCALS",TWO_ROUNDS) 
        Continue() 
END 

IF 
    ActuallyInCombat() 
	Global("B!Buff","LOCALS",0) 
THEN 
	RESPONSE #100 
	    ReallyForceSpell(Myself,WIZARD_MELF_METEOR) 
    	ReallyForceSpell(Myself,WIZARD_STONE_SKIN) 
        ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE) 
    	DisplayStringHead(Myself,46150) 
    	ReallyForceSpell(Myself,WIZARD_INVISIBILITY) 
    	SetGlobal("B!Buff","LOCALS",1) 
    	Continue() 
END 

IF 
    See(NearestEnemyOf(Myself)) 
    Class(NearestEnemyOf(Myself),FIGHTER_ALL) 
    !GlobalTimerNotExpired("B!CastSpell","LOCALS") 
    Global("CastRoE","LOCALS",0) 
THEN 
    RESPONSE #100 
        ForceSpell(NearestEnemyOf(Myself),WIZARD_RAY_OF_ENFEEBLEMENT) 
        SetGlobalTimer("B!CastSpell","LOCALS",ONE_ROUND) 
        SetGlobal("CastRoE","LOCALS",1) 
END 

IF 
    See(NearestEnemyOf(Myself)) 
	!StateCheck(Myself,STATE_INVISIBLE)
	!Range(NearestEnemyOf(Myself),30)
THEN
	RESPONSE #100
	    ForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
	    DisplayStringHead(Myself,46150) 
	    ReallyForceSpell(Myself,WIZARD_INVISIBILITY)
	    Continue()
END 

IF 
    See(NearestEnemyOf(Myself)) 
    !GlobalTimerNotExpired("B!CastSpell","LOCALS") 
	Global("B!Buff","LOCALS",1) 
	GlobalLT("MagicMissile","LOCALS",5)
THEN
	RESPONSE #100
	    ForceSpell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
        SetGlobalTimer("B!CastSpell","LOCALS",ONE_ROUND) 
	    IncrementGlobal("MagicMissile","LOCALS",1) 
	    Continue()
END

IF	
    See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	GlobalGT("MagicMissile","LOCALS",4) 
THEN
	RESPONSE #50
    	EquipMostDamagingMelee()
    	AttackReevaluate(NearestEnemyOf(Myself),15) 
    	Continue() 
	RESPONSE #50 
    	DisplayStringHead(Myself,46150) 
    	ReallyForceSpell(Myself,WIZARD_INVISIBILITY) 
    	SetGlobal("Melee","LOCALS",0) 
    	Continue() 
END

IF	
    See(NearestEnemyOf(Myself))
	StateCheck(Myself,STATE_INVISIBLE) 
	GlobalGT("MagicMissile","LOCALS",4) 
THEN
	RESPONSE #100
    	EquipMostDamagingMelee()
    	AttackReevaluate(NearestEnemyOf(Myself),15) 
    	Continue()
	RESPONSE #100
    	MoveToObject(SecondNearestEnemyOf(Myself)) 
    	Continue()
END
